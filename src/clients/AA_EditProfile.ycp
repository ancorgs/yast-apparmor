/* ------------------------------------------------------------------
*
*    Copyright (C) 2002-2006 Novell/SUSE
*
*    This program is free software; you can redistribute it and/or
*    modify it under the terms of version 2 of the GNU General Public
*    License published by the Free Software Foundation.
*
  ------------------------------------------------------------------*/
{
import "Wizard";
import "Popup";
import "Label";
import "Sequencer";
include "apparmor/apparmor_packages.ycp";
include "apparmor/apparmor_profile_check.ycp";
include "apparmor/profile_dialogs.ycp";
textdomain  "yast2-apparmor";

// Globalz



define any MainSequence() ``{

//
// Read the profiles from the SCR agent
   map profiles = (map) SCR::Read (.apparmor_profiles, "all");

   map aliases = $[
        "showProfile"        : ``(DisplayProfileForm(Settings["CURRENT_PROFILE"]:"", false)),
        "showHat"            : ``(DisplayProfileForm(Settings["CURRENT_HAT"]:"", true)),
        "chooseProfile"      : ``(SelectProfileForm(profiles, _("Please make a selection from the listed profiles and press Next to edit the profile."), _("Edit Profile - Choose profile to edit"), "apparmor_edit_profile" )),

   ];

   map sequence = $[
        "ws_start" : "chooseProfile",
        "chooseProfile" : $[
            `abort : `abort,
            `next : "showProfile",
            `finish : `next,
        ],
        "showProfile" : $[
            `abort : `abort,
            `next : `ws_finish,
            `showhat : "showHat",
            `finish : `next,
        ],
        "showHat" : $[
            `abort : `abort,
            `next : "showProfile",
            `finish : `next,
        ],
    ];

    Wizard::CreateDialog();
    Wizard::SetTitleIcon("apparmor_edit_profile");
    any ret = Sequencer::Run(aliases, sequence);
    Wizard::CloseDialog();
    Settings = remove( Settings, "CURRENT_PROFILE");
    Settings = remove( Settings, "PROFILE_MAP");
    return ret;
}



//
// YEAH BABY RUN BABY RUN
//
any ret = nil;

// no command line support #269891
if (size(WFM::Args()) > 0 )
{
    import "CommandLine";
    CommandLine::Init($[], WFM::Args());
    return ret;
}

if (!installAppArmorPackages()) {
  return ret;
}

if (!checkProfileSyntax()) {
  return ret;
}


ret = MainSequence();
return ret;
}

